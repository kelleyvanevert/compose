ws = _{ " " | "\t" | "\n" }

WHITESPACE = _{ ws }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

id = @{ ASCII_ALPHA_LOWER ~ ("_" | ASCII_ALPHANUMERIC)* }

sample_input = @{ "S" ~ ("a" ~ ("m" ~ ("p" ~ ("l" ~ ("e")?)?)?)?)? }
envelope_input = @{ "E" ~ ("n" ~ ("v" ~ ("e" ~ ("l" ~ ("o" ~ ("p" ~ ("e")?)?)?)?)?)?)? }
pattern_input = @{ "P" ~ ("a" ~ ("t" ~ ("t" ~ ("e" ~ ("r" ~ ("n")?)?)?)?)?)? }

number = @{
    | (ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)?)
    | ("." ~ ASCII_DIGIT+)
}

factor = ${ number ~ "f" }

params = { id ~ ("," ~ id)* ~ ","? }

function = { "fn" ~ "(" ~ params? ~ ")" ~ expr }

typename = { "sample" | "envelope" | "pattern" | "number" | "factor" }

primary = _{
    | factor
    | number
    | sample_input
    | envelope_input
    | pattern_input
    | function
    | id
    | block
    | ("(" ~ expr ~ ")")
}

expr         =   { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix* )* }
    infix    =  _{ add | sub | mul | div | pow }
      add    =   { "+" }
      sub    =   { "-" }
      mul    =   { "*" }
      div    =   { "/" }
      pow    =   { "^" }
    prefix   =  _{ pos | neg }
      pos    =   { "+" }
      neg    =   { "-" }
    postfix  =  _{ fac | coerce }
      fac    =   { "!" }
      coerce =   { "as" ~ typename }

stmt = _{ noop | def | ret }
    noop = { ";" }
    def = { "let" ~ id ~ "=" ~ expr ~ ";" }
    ret = { "return" ~ expr ~ ";" }

stmts = { stmt* }

block = { "{" ~ stmts ~ expr? ~ "}" }

entry_expression = _{ SOI ~ expr ~ EOI }

entry_statement = _{ SOI ~ stmt ~ EOI }
